IDENTIFICATION DIVISION.
PROGRAM-ID. RELATORIO-PAGAMENTOS.
ENVIRONMENT DIVISION.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
    SELECT ARQ-HISTORICO ASSIGN TO "HISTPAGTO.DAT"
        ORGANIZATION IS SEQUENTIAL
        ACCESS MODE IS SEQUENTIAL
        FILE STATUS IS FS-ARQ.

DATA DIVISION.
FILE SECTION.
FD ARQ-HISTORICO.
01 HIST-REG.
    05 H-NUM-DOC    PIC 9(10).
    05 H-CNPJ-FORN  PIC 9(14).
    05 H-DATA-PGTO  PIC 9(8).
    05 H-VALOR-PAGO PIC 9(10)V99.

WORKING-STORAGE SECTION.
01 WS-VALOR-FORMATADO   PIC $(15)9.99.
LOCAL-STORAGE SECTION.
    01 FS-ARQ PIC XX.
    01 WS-LINE              PIC 99 VALUE 6.
    01 WS-VALOR-RAW         PIC 9(15)V99.
    01 WS-VALOR-F-TRIM      PIC X(18).
    01 WS-TOTAL-REG         PIC 999 VALUE 0.
    01 WS-CONFIRMA          PIC X.
    01 WS-FIM-ARQUIVO       PIC X VALUE 'N'.
        88 FIM-ARQUIVO VALUE 'S'.

    01 WS-HIST-REG  OCCURS 0 TO 999 DEPENDING ON WS-TOTAL-REG
                    INDEXED BY IDX.
        05 WS-NUM-DOC         PIC 9(10).
        05 WS-CNPJ-FORN       PIC 9(14).
        05 WS-DATA-PGTO       PIC 9(8).
        05 WS-VALOR-PAGO      PIC 9(10)V99.
        05 WS-VALOR           PIC X(13).
        05 WS-PERIODO         PIC 9(6).
        05 WS-DIA             PIC 9(2).

    01 LINHA-TRACEJADA      PIC X(50) VALUE ALL "-".
    01 WS-PERIODO-ATUAL     PIC 9(6).
    01 WS-MES               PIC 99.
    01 WS-PERIODO-ATUAL-F   PIC X(11).
    01 WS-VALOR-PERIODO     PIC 9(15)V99 VALUE 0.
    01 WS-TOTAL-PERIODO     PIC 999      VALUE 0.
    01 WS-MES-NOME          PIC XXX.

SCREEN SECTION.
01 TELA-BRANCA.
    05 BLANK SCREEN.

01 TELA-PROX-PAGINA.
    05 LINE WS-LINE COLUMN 1 VALUE "APERTE ENTER PARA IR PARA A PROXIMA PAGINA...".
    05 LINE WS-LINE COLUMN PLUS 1 PIC X TO WS-CONFIRMA.


PROCEDURE DIVISION.
MAIN-PROCEDURE.
    PERFORM ABRIR-ARQUIVOS.
    PERFORM LER-PAGAMENTO UNTIL FIM-ARQUIVO.

    PERFORM EMITIR-RELATORIO.

    PERFORM FECHAR-ARQUIVOS.
    EXIT PROGRAM.

ABRIR-ARQUIVOS.
    OPEN INPUT ARQ-HISTORICO.
    IF FS-ARQ NOT = "00"
        DISPLAY TELA-BRANCA
        DISPLAY "ERRO AO ABRIR ARQUIVO: " FS-ARQ
        STOP RUN
    END-IF.

FECHAR-ARQUIVOS.
    CLOSE ARQ-HISTORICO.

IMPRIME-CABECALHO-RELATORIO.
    DISPLAY "PERIODO"           AT LINE WS-LINE COLUMN 1.
    PERFORM FORMATAR-PERIODO.
    DISPLAY WS-PERIODO-ATUAL-F  AT LINE WS-LINE COLUMN 9.
    ADD 1 TO WS-LINE.
    DISPLAY LINHA-TRACEJADA     AT LINE WS-LINE COLUMN 1.
    ADD 1 TO WS-LINE.
    DISPLAY "NUM DOC"           AT LINE WS-LINE COLUMN 1
    DISPLAY "|CNPJ FORN"        AT LINE WS-LINE COLUMN 12
    DISPLAY "|VALOR"            AT LINE WS-LINE COLUMN 27
    DISPLAY "|DIA"              AT LINE WS-LINE COLUMN 41
    ADD 1 TO WS-LINE
    PERFORM IMPRIME-LINHA-DIVISORIA.
    ADD 1 TO WS-LINE.

IMPRIME-RODAPE-RELATORIO.
    DISPLAY LINHA-TRACEJADA     AT LINE WS-LINE COLUMN 1.
    ADD 1 TO WS-LINE.
    DISPLAY "VALOR TOTAL ="       AT LINE WS-LINE COLUMN 1.
    MOVE WS-VALOR-PERIODO TO WS-VALOR-RAW
    PERFORM FORMATAR-VALOR
    DISPLAY WS-VALOR-F-TRIM     AT LINE WS-LINE COLUMN 15.
    ADD 1 TO WS-LINE.
    DISPLAY LINHA-TRACEJADA     AT LINE WS-LINE COLUMN 1.

IMPRIME-LINHA-DIVISORIA.
    DISPLAY LINHA-TRACEJADA AT LINE WS-LINE COLUMN 1.
    DISPLAY "|" AT LINE WS-LINE COLUMN 12.
    DISPLAY "|" AT LINE WS-LINE COLUMN 27.
    DISPLAY "|" AT LINE WS-LINE COLUMN 41.

EMITIR-RELATORIO.
    SORT WS-HIST-REG
        ON ASCENDING KEY WS-DATA-PGTO.
    MOVE WS-PERIODO(1) TO WS-PERIODO-ATUAL.
    MOVE 1 TO WS-TOTAL-PERIODO.
    MOVE WS-VALOR-PAGO(1) TO WS-VALOR-PERIODO.

    MOVE 1 TO WS-LINE
    PERFORM IMPRIME-CABECALHO-RELATORIO.

    PERFORM VARYING IDX FROM 1 BY 1 UNTIL IDX > WS-TOTAL-REG
        IF WS-LINE > 20
            DISPLAY TELA-PROX-PAGINA
            ACCEPT  TELA-PROX-PAGINA
            MOVE 2 TO WS-LINE
            DISPLAY TELA-BRANCA
        END-IF
        IF WS-PERIODO(IDX) > WS-PERIODO-ATUAL
            PERFORM IMPRIME-RODAPE-RELATORIO
            ADD 1 to WS-LINE
            MOVE WS-PERIODO(IDX) TO WS-PERIODO-ATUAL
            MOVE 1 TO WS-TOTAL-PERIODO
            MOVE WS-VALOR-PAGO(IDX) TO WS-VALOR-PERIODO
            ADD 1 TO WS-LINE
            PERFORM IMPRIME-CABECALHO-RELATORIO
        END-IF
        DISPLAY WS-NUM-DOC(IDX)     AT LINE WS-LINE COLUMN 2
        DISPLAY "|"                 AT LINE WS-LINE COLUMN 12
        DISPLAY WS-CNPJ-FORN(IDX)   AT LINE WS-LINE COLUMN 13
        DISPLAY "|"                 AT LINE WS-LINE COLUMN 27
        DISPLAY WS-VALOR(IDX)       AT LINE WS-LINE COLUMN 28
        DISPLAY "|"                 AT LINE WS-LINE COLUMN 41
        DISPLAY WS-DIA(IDX)         AT LINE WS-LINE COLUMN 42
        ADD 1 TO WS-LINE
        PERFORM IMPRIME-LINHA-DIVISORIA
        ADD 1 TO WS-LINE
    END-PERFORM.
    PERFORM IMPRIME-RODAPE-RELATORIO.
    ADD 1 TO WS-LINE.
    ACCEPT WS-CONFIRMA AT LINE WS-LINE COLUMN 1.

LER-PAGAMENTO.
    READ ARQ-HISTORICO
        AT END
            MOVE "S" TO WS-FIM-ARQUIVO
        NOT AT END
            ADD 1 TO WS-TOTAL-REG
            MOVE HIST-REG TO WS-HIST-REG(WS-TOTAL-REG)
            MOVE WS-VALOR-PAGO(WS-TOTAL-REG) TO WS-VALOR-RAW
            PERFORM FORMATAR-VALOR
            MOVE WS-VALOR-F-TRIM TO WS-VALOR(WS-TOTAL-REG)
            PERFORM SEPARAR-DATA
    END-READ.

TRADUZIR-MES.
    EVALUATE WS-MES
        WHEN 1
            MOVE "JAN" TO WS-MES-NOME
        WHEN 2
            MOVE "FEV" TO WS-MES-NOME
        WHEN 3
            MOVE "MAR" TO WS-MES-NOME
        WHEN 4
            MOVE "ABR" TO WS-MES-NOME
        WHEN 5
            MOVE "MAI" TO WS-MES-NOME
        WHEN 6
            MOVE "JUN" TO WS-MES-NOME
        WHEN 7
            MOVE "JUL" TO WS-MES-NOME
        WHEN 8
            MOVE "AGO" TO WS-MES-NOME
        WHEN 9
            MOVE "SET" TO WS-MES-NOME
        WHEN 10
            MOVE "OUT" TO WS-MES-NOME
        WHEN 11
            MOVE "NOV" TO WS-MES-NOME
        WHEN 12
            MOVE "DEZ" TO WS-MES-NOME
        WHEN OTHER
            MOVE "ERR" TO WS-MES-NOME
    END-EVALUATE.

FORMATAR-PERIODO.
    MOVE WS-PERIODO-ATUAL(5:2) TO WS-MES.
    PERFORM TRADUZIR-MES.
    STRING
        WS-MES-NOME           DELIMITED BY SIZE
        " DE "                DELIMITED BY SIZE
        WS-PERIODO-ATUAL(1:4) DELIMITED BY SIZE
        INTO WS-PERIODO-ATUAL-F
    END-STRING.

SEPARAR-DATA.
    MOVE H-DATA-PGTO(7:2) TO WS-DIA(WS-TOTAL-REG)
    MOVE H-DATA-PGTO(1:6) TO WS-PERIODO(WS-TOTAL-REG).

FORMATAR-VALOR.
    MOVE WS-VALOR-RAW TO WS-VALOR-FORMATADO
    MOVE FUNCTION TRIM(WS-VALOR-FORMATADO LEADING) TO WS-VALOR-F-TRIM.
