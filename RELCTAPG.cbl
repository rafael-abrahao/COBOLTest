IDENTIFICATION DIVISION.
PROGRAM-ID. RELATORIO-DE-CONTAS.
ENVIRONMENT DIVISION.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
    SELECT ARQ-CONTAS ASSIGN TO "CONTAPAGAR.DAT"
        ORGANIZATION IS INDEXED
        ACCESS MODE IS SEQUENTIAL
        RECORD KEY IS CP-CHAVE
        FILE STATUS IS FS-ARQ.

    SELECT ARQ-FORNECEDORES ASSIGN TO "FORNECEDOR.DAT"
        ORGANIZATION IS INDEXED
        ACCESS MODE IS RANDOM
        RECORD KEY IS F-CNPJ
        FILE STATUS IS FS-ARQ.

DATA DIVISION.
FILE SECTION.
FD ARQ-CONTAS.
01 CONTA-REG.
    05 CP-CHAVE.
        10 CP-NUM-DOC   PIC 9(10).
        10 CP-CNPJ-FORN PIC 9(14).
    05 CP-DATA-EMISSAO  PIC 9(8).       *>(AAAAMMDD)
    05 CP-DATA-VENC     PIC 9(8).
    05 CP-VALOR         PIC 9(10)V99.
    05 CP-SITUACAO      PIC X.          *>(A=ABERTO, P=PAGO, C=CANCELADO)
        88 CP-SIT-ABERTO    VALUE "A".
        88 CP-SIT-PAGO      VALUE "P".
        88 CP-SIT-CANCELADO VALUE "C".
    05 CP-DATA-PGTO     PIC 9(8).

FD ARQ-FORNECEDORES.
01 FORNECEDOR-REG.
    05 F-CNPJ           PIC 9(14).
    05 F-RAZAO-SOCIAL   PIC X(40).
    05 F-ENDERECO       PIC X(50).
    05 F-TELEFONE       PIC 9(11).
    05 F-EMAIL          PIC X(30).
    05 F-STATUS PIC 9 VALUE 1.
        88 F-ATIVO VALUE 1.
        88 F-INATIVO VALUE 0.

WORKING-STORAGE SECTION.
01 WS-VALOR-FORMATADO   PIC $(10)9.99.
01 WS-VALOR-FORMATADO-M PIC $(15)9.99.

LOCAL-STORAGE SECTION.
01 FS-ARQ               PIC XX.
01 WS-CONFIRMA          PIC X.
01 WS-OPCAO             PIC 9.

01 WS-VALOR-TRIM        PIC X(13).

01 WS-ANO               PIC 9(4).
01 WS-MES               PIC 9(2).
01 WS-DIA               PIC 9(2).
01 WS-DATA-ORIGINAL     PIC 9(8).
01 WS-DATA-FORMATADA    PIC X(10).

01 WS-RAZ-SOC-PEQ       PIC X(20).

01 WS-LINE              PIC 99 VALUE 4.
01 LINHA-TRACEJADA      PIC X(73) VALUE ALL "-".
01 LINHA-TRACEJADA-M    PIC X(84) VALUE ALL "-".

01 WS-FIM               PIC X VALUE SPACE.
    88 FIM-ARQUIVO VALUE "S".

01 WS-RELATORIO.
    05 WS-CONTA OCCURS 3 TIMES              *> (PROX, MENOR VALOR, MAIOR VALOR)
                INDEXED BY IDX.
        10 WS-CHAVE.
            15 WS-NUM-DOC   PIC 9(10).
            15 WS-CNPJ-FORN PIC 9(14).
        10 WS-DATA-EMISSAO  PIC 9(8).       *>(AAAAMMDD)
        10 WS-DATA-VENC     PIC 9(8).
        10 WS-VALOR         PIC 9(10)V99.
        10 WS-SITUACAO      PIC X.          *>(A=ABERTO, P=PAGO, C=CANCELADO)
        10 WS-DATA-PGTO     PIC 9(8).
    05 WS-VALOR-TOTAL       PIC 9(15)V99.
    05 WS-VALOR-MEDIO       PIC 9(10)V99.
    05 WS-VALOR-TOTAL-F     PIC X(18).
    05 WS-VALOR-MEDIO-F     PIC X(13).
    05 WS-QTD-REG           PIC 999 VALUE 0.

LINKAGE SECTION.
SCREEN SECTION.
01 TELA-BRANCA.
    05 BLANK SCREEN.

01 TELA-CABECALHO-LISTAGEM.
    05 LINE 2 COLUMN 1  VALUE "NUMERO".
    05 LINE 2 COLUMN 11 VALUE "|FORNECEDOR".
    05 LINE 2 COLUMN 32 VALUE "|DATA EMIS".
    05 LINE 2 COLUMN 43 VALUE "|DATA VENC".
    05 LINE 2 COLUMN 54 VALUE "|VALOR".
    05 LINE 3 COLUMN 1  FROM LINHA-TRACEJADA.
    05 LINE 3 COLUMN 11 VALUE "|".
    05 LINE 3 COLUMN 32 VALUE "|".
    05 LINE 3 COLUMN 43 VALUE "|".
    05 LINE 3 COLUMN 54 VALUE "|".


01 TELA-CABECALHO-RELATORIO.
    05 LINE 2 COLUMN 12 VALUE "|NUMERO".
    05 LINE 2 COLUMN 22 VALUE "|FORNECEDOR".
    05 LINE 2 COLUMN 43 VALUE "|DATA EMIS".
    05 LINE 2 COLUMN 54 VALUE "|DATA VENC".
    05 LINE 2 COLUMN 65 VALUE "|VALOR".
    05 LINE 3 COLUMN 1  FROM LINHA-TRACEJADA-M.
    05 LINE 3 COLUMN 12 VALUE "|".
    05 LINE 3 COLUMN 22 VALUE "|".
    05 LINE 3 COLUMN 43 VALUE "|".
    05 LINE 3 COLUMN 54 VALUE "|".
    05 LINE 3 COLUMN 65 VALUE "|".
    05 LINE 4 COLUMN 1 VALUE "PROX CONTA |".
    05 LINE 6 COLUMN 1 VALUE "MENOR VALOR|".
    05 LINE 8 COLUMN 1 VALUE "MAIOR VALOR|".

01 TELA-RODAPE-RELATORIO.
    05 LINE 11 COLUMN 1 VALUE "--------------------|---------------------------------------------------------------".
    05 LINE 12 COLUMN 1 VALUE "TOTAL CONTAS ABERTAS|".
    05 LINE 12 COLUMN PLUS 1 PIC 999 FROM WS-QTD-REG.
    05 LINE 13 COLUMN 1 VALUE "--------------------|---------------------------------------------------------------".
    05 LINE 14 COLUMN 1 VALUE "VALOR TOTAL         |".
    05 LINE 14 COLUMN PLUS 1 PIC X(18) FROM WS-VALOR-TOTAL-F.
    05 LINE 15 COLUMN 1 VALUE "--------------------|---------------------------------------------------------------".
    05 LINE 16 COLUMN 1 VALUE "VALOR MEDIO         |".
    05 LINE 16 COLUMN PLUS 1 PIC X(13) FROM WS-VALOR-MEDIO-F.
    05 LINE 17 COLUMN 1 VALUE "--------------------|---------------------------------------------------------------".
    05 LINE 18 COLUMN 1 VALUE "------------------------------------------------------------------------------------".

01 TELA-MENU.
    05 BLANK SCREEN.
    05 LINE 6  COLUMN 28 VALUE "1- LISTAR CONTAS.".
    05 LINE 8  COLUMN 28 VALUE "2- EMITIR RELATORIO.".
    05 LINE 11 COLUMN 28 VALUE "ESCOLHA UMA OPCAO: ".
    05 LINE 11 COLUMN 50 PIC 9 TO WS-OPCAO.

01 TELA-PROX-PAGINA.
    05 LINE WS-LINE COLUMN 1 VALUE "APERTE ENTER PARA IR PARA A PROXIMA PAGINA...".
    05 LINE WS-LINE COLUMN PLUS 1 PIC X TO WS-CONFIRMA.

PROCEDURE DIVISION.
MAIN-PROCEDURE.

    OPEN INPUT ARQ-CONTAS
    IF FS-ARQ NOT = "00"
        DISPLAY "ERRO AO ABRIR ARQUIVO: " FS-ARQ
        STOP RUN
    END-IF
    OPEN INPUT ARQ-FORNECEDORES
    IF FS-ARQ NOT = "00"
        DISPLAY "ERRO AO ABRIR ARQUIVO: " FS-ARQ
        STOP RUN
    END-IF.

    DISPLAY TELA-MENU.
    ACCEPT  TELA-MENU.

    EVALUATE WS-OPCAO
        WHEN 1
            DISPLAY TELA-BRANCA
            PERFORM LISTAR-CONTAS
        WHEN 2
            PERFORM GERAR-RELATORIO
            PERFORM IMPRIMIR-RELATORIO
        WHEN OTHER
            DISPLAY TELA-BRANCA
            DISPLAY "OPCAO INVALIDA"
    END-EVALUATE.

    CLOSE ARQ-FORNECEDORES.
    CLOSE ARQ-CONTAS.

    EXIT PROGRAM.


IMPRIMIR-RELATORIO.
    MOVE WS-VALOR-TOTAL TO WS-VALOR-FORMATADO-M
    MOVE FUNCTION TRIM(WS-VALOR-FORMATADO-M LEADING) TO WS-VALOR-TOTAL-F

    MOVE WS-VALOR-MEDIO TO WS-VALOR-FORMATADO
    MOVE FUNCTION TRIM(WS-VALOR-FORMATADO LEADING) TO WS-VALOR-MEDIO-F

    DISPLAY TELA-CABECALHO-RELATORIO
    PERFORM VARYING IDX FROM 1 BY 1 UNTIL IDX > 3

        DISPLAY "|" AT LINE WS-LINE COLUMN 12

        DISPLAY WS-NUM-DOC(IDX) AT LINE WS-LINE COLUMN 13

        DISPLAY "|" AT LINE WS-LINE COLUMN 22

        MOVE WS-CNPJ-FORN(IDX) TO F-CNPJ
        READ ARQ-FORNECEDORES KEY IS F-CNPJ
            INVALID KEY
                DISPLAY "ERRO: CHAVE INVALIDA"
                STOP RUN
            NOT INVALID KEY
                MOVE F-RAZAO-SOCIAL TO WS-RAZ-SOC-PEQ
                DISPLAY WS-RAZ-SOC-PEQ AT LINE WS-LINE COLUMN 23
        END-READ

        DISPLAY "|" AT LINE WS-LINE COLUMN 43

        MOVE WS-DATA-EMISSAO(IDX) TO WS-DATA-ORIGINAL
        PERFORM FORMATAR-DATA
        DISPLAY WS-DATA-FORMATADA AT LINE WS-LINE COLUMN 44

        DISPLAY "|" AT LINE WS-LINE COLUMN 54

        MOVE WS-DATA-VENC(IDX) TO WS-DATA-ORIGINAL
        PERFORM FORMATAR-DATA
        DISPLAY WS-DATA-FORMATADA AT LINE WS-LINE COLUMN 55

        DISPLAY "|" AT LINE WS-LINE COLUMN 65

        MOVE WS-VALOR(IDX) TO WS-VALOR-FORMATADO
        MOVE FUNCTION TRIM(WS-VALOR-FORMATADO LEADING) TO WS-VALOR-TRIM
        DISPLAY WS-VALOR-TRIM AT LINE WS-LINE COLUMN 66

        ADD 1 TO WS-LINE
        DISPLAY LINHA-TRACEJADA-M AT LINE WS-LINE COLUMN 1
        DISPLAY "|" AT LINE WS-LINE COLUMN 12
        DISPLAY "|" AT LINE WS-LINE COLUMN 22
        DISPLAY "|" AT LINE WS-LINE COLUMN 43
        DISPLAY "|" AT LINE WS-LINE COLUMN 54
        DISPLAY "|" AT LINE WS-LINE COLUMN 65
        ADD 1 TO WS-LINE
    END-PERFORM.
    DISPLAY LINHA-TRACEJADA-M AT LINE WS-LINE COLUMN 1.
    DISPLAY TELA-RODAPE-RELATORIO.
    DISPLAY " " AT LINE 19 COLUMN 1.
    ACCEPT WS-CONFIRMA AT LINE 20 COLUMN 1.


GERAR-RELATORIO.

    MOVE 99999999 TO WS-DATA-VENC(1).
    MOVE 9999999999.99 TO WS-VALOR(2).
    MOVE 0 TO WS-VALOR(3).

    PERFORM UNTIL FIM-ARQUIVO
        READ ARQ-CONTAS NEXT RECORD
            AT END
                MOVE "S" TO WS-FIM
            NOT AT END
                IF CP-SIT-ABERTO
                    ADD 1 TO WS-QTD-REG
                    ADD CP-VALOR TO WS-VALOR-TOTAL
                    IF CP-DATA-VENC < WS-DATA-VENC(1)
                        MOVE CONTA-REG TO WS-CONTA(1)
                    END-IF
                    IF CP-VALOR < WS-VALOR(2)
                        MOVE CONTA-REG TO WS-CONTA(2)
                    END-IF
                    IF CP-VALOR > WS-VALOR(3)
                        MOVE CONTA-REG TO WS-CONTA(3)
                    END-IF
                END-IF
        END-READ
    END-PERFORM.
    COMPUTE WS-VALOR-MEDIO = WS-VALOR-TOTAL / WS-QTD-REG.

LISTAR-CONTAS.
    DISPLAY TELA-CABECALHO-LISTAGEM.
    PERFORM UNTIL FIM-ARQUIVO
        READ ARQ-CONTAS NEXT RECORD
            AT END
                MOVE "S" TO WS-FIM
            NOT AT END
                IF CP-SIT-ABERTO
                    MOVE CP-CNPJ-FORN TO F-CNPJ
                    READ ARQ-FORNECEDORES KEY IS F-CNPJ
                    PERFORM IMPRIMIR-CONTA
                    IF WS-LINE > 22
                        ADD 1 TO WS-LINE
                        DISPLAY TELA-PROX-PAGINA
                        ACCEPT  TELA-PROX-PAGINA
                        MOVE 4 TO WS-LINE
                        DISPLAY TELA-BRANCA
                        DISPLAY TELA-CABECALHO-LISTAGEM
                    END-IF
                END-IF
        END-READ
    END-PERFORM.
    DISPLAY LINHA-TRACEJADA AT LINE WS-LINE COLUMN 1.
    ADD 1 TO WS-LINE.
    ACCEPT WS-CONFIRMA AT LINE WS-LINE COLUMN 1.

FORMATAR-DATA.
    MOVE WS-DATA-ORIGINAL(1:4) TO WS-ANO
    MOVE WS-DATA-ORIGINAL(5:2) TO WS-MES
    MOVE WS-DATA-ORIGINAL(7:2) TO WS-DIA

    STRING
        WS-DIA DELIMITED BY SIZE
        "/"    DELIMITED BY SIZE
        WS-MES DELIMITED BY SIZE
        "/"    DELIMITED BY SIZE
        WS-ANO DELIMITED BY SIZE
        INTO WS-DATA-FORMATADA
    END-STRING.

IMPRIMIR-CONTA.
    DISPLAY CP-NUM-DOC AT LINE WS-LINE COLUMN 1

    DISPLAY "|" AT LINE WS-LINE COLUMN 11

    MOVE F-RAZAO-SOCIAL TO WS-RAZ-SOC-PEQ.
    DISPLAY WS-RAZ-SOC-PEQ AT LINE WS-LINE COLUMN 12

    DISPLAY "|" AT LINE WS-LINE COLUMN 32

    MOVE CP-DATA-EMISSAO TO WS-DATA-ORIGINAL
    PERFORM FORMATAR-DATA
    DISPLAY WS-DATA-FORMATADA AT LINE WS-LINE COLUMN 33

    DISPLAY "|" AT LINE WS-LINE COLUMN 43

    MOVE CP-DATA-VENC TO WS-DATA-ORIGINAL
    PERFORM FORMATAR-DATA
    DISPLAY WS-DATA-FORMATADA AT LINE WS-LINE COLUMN 44

    DISPLAY "|" AT LINE WS-LINE COLUMN 54

    MOVE CP-VALOR TO WS-VALOR-FORMATADO
    MOVE FUNCTION TRIM(WS-VALOR-FORMATADO LEADING) TO WS-VALOR-TRIM
    DISPLAY WS-VALOR-TRIM AT LINE WS-LINE COLUMN 55

    ADD 1 TO WS-LINE
    DISPLAY LINHA-TRACEJADA AT LINE WS-LINE COLUMN 1
    DISPLAY "|" AT LINE WS-LINE COLUMN 11
    DISPLAY "|" AT LINE WS-LINE COLUMN 32
    DISPLAY "|" AT LINE WS-LINE COLUMN 43
    DISPLAY "|" AT LINE WS-LINE COLUMN 54
    ADD 1 TO WS-LINE.
