IDENTIFICATION DIVISION.
PROGRAM-ID. PAGAMENTO-DE-CONTAS.
ENVIRONMENT DIVISION.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
    SELECT ARQ-CONTAS ASSIGN TO "CONTAPAGAR.DAT"
        ORGANIZATION IS INDEXED
        ACCESS MODE IS DYNAMIC
        RECORD KEY IS CP-CHAVE
        FILE STATUS IS FS-ARQ.

    SELECT ARQ-HISTORICO ASSIGN TO "HISTPAGTO.DAT"
        ORGANIZATION IS SEQUENTIAL
        FILE STATUS IS FS-ARQ.

DATA DIVISION.
FILE SECTION.
FD ARQ-CONTAS.
01 CONTA-REG.
    05 CP-CHAVE.
        10 CP-NUM-DOC   PIC 9(10).
        10 CP-CNPJ-FORN PIC 9(14).
    05 CP-DATA-EMISSAO  PIC 9(8).       *>(AAAAMMDD)
    05 CP-DATA-VENC     PIC 9(8).
    05 CP-VALOR         PIC 9(10)V99.
    05 CP-SITUACAO      PIC X.          *>(A=ABERTO, P=PAGO, C=CANCELADO)
        88 CP-SIT-ABERTO    VALUE "A".
        88 CP-SIT-PAGO      VALUE "P".
        88 CP-SIT-CANCELADO VALUE "C".
    05 CP-DATA-PGTO     PIC 9(8).

FD ARQ-HISTORICO.
01 HIST-REG.
    05 H-NUM-DOC    PIC 9(10).
    05 H-CNPJ-FORN  PIC 9(14).
    05 H-DATA-PGTO  PIC 9(8).
    05 H-VALOR-PAGO PIC 9(10)V99.

WORKING-STORAGE SECTION.
01 FS-ARQ PIC XX.
01 WS-MENSAGEM PIC X(45).
01 WS-CONFIRMA PIC X.

SCREEN SECTION.
01 TELA-BRANCA.
    05 BLANK SCREEN.

01 TELA-BUSCAR-CONTA.
    05 BLANK SCREEN.
    05 LINE 3    COLUMN 30 VALUE "BUSCAR CONTA.".
    05 LINE 6    COLUMN 28 VALUE "NUMERO DO DOC:".
    05 LINE 6    COLUMN 60 PIC 9(10) TO CP-NUM-DOC.
    05 LINE 8    COLUMN 28 VALUE "CNPJ DO FORNECEDOR:".
    05 LINE 8    COLUMN 60 PIC 9(14) TO CP-CNPJ-FORN.

01 TELA-CONFIRMAR-PGTO.
    05 BLANK SCREEN.
    05 LINE 6    COLUMN 28 VALUE "NUMERO DO DOC:".
    05 LINE 6    COLUMN 60 PIC 9(10) FROM CP-NUM-DOC.
    05 LINE 8    COLUMN 28 VALUE "CNPJ DO FORNECEDOR:".
    05 LINE 8    COLUMN 60 PIC 9(14) FROM CP-CNPJ-FORN.
    05 LINE 10   COLUMN 28 VALUE "DATA DE EMISSAO (AAAAMMDD):".
    05 LINE 10   COLUMN 60 PIC 9(8) FROM CP-DATA-EMISSAO.
    05 LINE 12   COLUMN 28 VALUE "DATA DE VENCIMENTO (AAAAMMDD):".
    05 LINE 12   COLUMN 60 PIC 9(8) FROM CP-DATA-VENC.
    05 LINE 16   COLUMN 28 VALUE "VALOR:".
    05 LINE 16   COLUMN PLUS 1 PIC $(10)9.99 FROM CP-VALOR.
    05 LINE 20   COLUMN 28 VALUE "DESEJA REGISTRAR O PAGAMENTO DESTA CONTA?(S/N)".
    05 LINE 20   COLUMN PLUS 2 PIC X TO WS-CONFIRMA.

01 TELA-DATA.
    05 BLANK SCREEN.
    05 LINE 6    COLUMN 28 VALUE "INFORME A DATA DO PAGAMENTO (AAAAMMDD):".
    05 LINE 6    COLUMN PLUS 2 PIC 9(8) TO CP-DATA-PGTO.

01 TELA-ERRO.
    02 BLANK SCREEN.
    02 LINE 6 COLUMN 28     VALUE "ERRO:".
    02 LINE 6 COLUMN PLUS 1 PIC X(45) FROM WS-MENSAGEM.
    02 LINE 8 COLUMN 28     VALUE "PRESSIONE ENTER PARA CONTINUAR.".
    02 LINE 8 COLUMN PLUS 2 PIC X TO WS-CONFIRMA.

01 TELA-FINALIZAR.
    02 BLANK SCREEN.
    02 LINE 6 COLUMN 28     VALUE "PAGAMENTO REGISTRADO COM SUCESSO.".
    02 LINE 8 COLUMN 28     VALUE "PRESSIONE ENTER PARA CONTINUAR.".
    02 LINE 8 COLUMN PLUS 2 PIC X TO WS-CONFIRMA.


PROCEDURE DIVISION.
MAIN-PROCEDURE.
    PERFORM ABRIR-ARQUIVO-CONTAS.

    DISPLAY TELA-BUSCAR-CONTA.
    ACCEPT  TELA-BUSCAR-CONTA.

    READ ARQ-CONTAS KEY IS CP-CHAVE
        INVALID KEY
            MOVE "CONTA NAO ENCONTRADA" TO WS-MENSAGEM
            DISPLAY TELA-ERRO
            ACCEPT  TELA-ERRO
            STOP RUN
        NOT INVALID KEY
            IF NOT CP-SIT-ABERTO
                MOVE "CONTA NAO ESTA ABERTA" TO WS-MENSAGEM
                DISPLAY TELA-ERRO
                ACCEPT  TELA-ERRO
                STOP RUN
            END-IF
            DISPLAY TELA-CONFIRMAR-PGTO
            ACCEPT  TELA-CONFIRMAR-PGTO
            IF NOT WS-CONFIRMA = "S" AND NOT WS-CONFIRMA = "N"
                MOVE "OPCAO INVALIDA" TO WS-MENSAGEM
                DISPLAY TELA-ERRO
                ACCEPT TELA-ERRO
                STOP RUN
            END-IF
            IF WS-CONFIRMA = "S"
                DISPLAY TELA-DATA
                ACCEPT  TELA-DATA
                PERFORM VALIDAR-DATA
                PERFORM REGISTRAR-PAGEMENTO
                DISPLAY TELA-FINALIZAR
                ACCEPT  TELA-FINALIZAR
            END-IF
    END-READ.

    CLOSE ARQ-CONTAS.
    EXIT PROGRAM.

ABRIR-ARQUIVO-CONTAS.
    OPEN I-O ARQ-CONTAS.
    IF FS-ARQ = "35"
        OPEN OUTPUT ARQ-CONTAS
        CLOSE ARQ-CONTAS
        OPEN I-O ARQ-CONTAS
    END-IF.
    IF FS-ARQ NOT = "00"
        DISPLAY TELA-BRANCA
        DISPLAY "ERRO AO ABRIR ARQUIVO: " FS-ARQ
        STOP RUN
    END-IF.

VALIDAR-DATA.
    IF CP-DATA-PGTO < CP-DATA-EMISSAO OR CP-DATA-PGTO > CP-DATA-VENC
        MOVE "DATA DE PAGAMENTO INVALIDA" TO WS-MENSAGEM
        DISPLAY TELA-ERRO
        ACCEPT  TELA-ERRO
        STOP RUN
    END-IF.

REGISTRAR-PAGEMENTO.
    OPEN EXTEND ARQ-HISTORICO.
    IF FS-ARQ = "35"
        OPEN OUTPUT ARQ-HISTORICO
        CLOSE ARQ-HISTORICO
        OPEN EXTEND ARQ-HISTORICO
    END-IF.
    IF FS-ARQ NOT = "00"
        DISPLAY TELA-BRANCA
        DISPLAY "ERRO AO ABRIR ARQUIVO: " FS-ARQ
        STOP RUN
    END-IF.

    MOVE CP-NUM-DOC   TO H-NUM-DOC.
    MOVE CP-CNPJ-FORN TO H-CNPJ-FORN.
    MOVE CP-DATA-PGTO TO H-DATA-PGTO.
    MOVE CP-VALOR     TO H-VALOR-PAGO.

    WRITE HIST-REG.

    MOVE "P" TO CP-SITUACAO.
    REWRITE CONTA-REG.

    CLOSE ARQ-HISTORICO.
