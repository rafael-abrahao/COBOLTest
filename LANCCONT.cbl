IDENTIFICATION DIVISION.
PROGRAM-ID. LANCAMENTO-DE-CONTAS.
ENVIRONMENT DIVISION.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
    SELECT ARQ-CONTAS ASSIGN TO "CONTAPAGAR.DAT"
    ORGANIZATION IS INDEXED
    ACCESS MODE IS DYNAMIC
    RECORD KEY IS CP-CHAVE
    FILE STATUS IS FS-ARQ.

    SELECT ARQ-FORNECEDORES ASSIGN TO "FORNECEDOR.DAT"
    ORGANIZATION IS INDEXED
    ACCESS MODE IS DYNAMIC
    RECORD KEY IS F-CNPJ
    FILE STATUS IS FS-ARQ.

DATA DIVISION.
FILE SECTION.
FD ARQ-CONTAS.
01 CONTA-REG.
    05 CP-CHAVE.
        10 CP-NUM-DOC   PIC 9(10).
        10 CP-CNPJ-FORN PIC 9(14).
    05 CP-DATA-EMISSAO  PIC 9(8).       *>(AAAAMMDD)
    05 CP-DATA-VENC     PIC 9(8).
    05 CP-VALOR         PIC 9(10)V99.
    05 CP-SITUACAO      PIC X.          *>(A=ABERTO, P=PAGO, C=CANCELADO)
        88 CP-SIT-ABERTO    VALUE "A".
        88 CP-SIT-PAGO      VALUE "P".
        88 CP-SIT-CANCELADO VALUE "C".
    05 CP-DATA-PGTO     PIC 9(8).

FD ARQ-FORNECEDORES.
01 FORNECEDOR-REG.
    05 F-CNPJ           PIC 9(14).
    05 F-RAZAO-SOCIAL   PIC X(40).
    05 F-ENDERECO       PIC X(50).
    05 F-TELEFONE       PIC 9(11).
    05 F-EMAIL          PIC X(30).
    05 F-STATUS PIC 9 VALUE 1.
        88 F-ATIVO VALUE 1.
        88 F-INATIVO VALUE 0.

WORKING-STORAGE SECTION.
01 FS-ARQ PIC XX.
01 WS-MENSAGEM PIC X(45).
01 WS-CONFIRMA PIC X.
01 WS-DIA-EMISSAO       PIC 99.
01 WS-MES-EMISSAO       PIC 99.
01 WS-ANO-EMISSAO       PIC 9999.
01 WS-DIA-VENC          PIC 99.
01 WS-MES-VENC          PIC 99.
01 WS-ANO-VENC          PIC 9999.

SCREEN SECTION.
01 TELA-BRANCA.
    05 BLANK SCREEN.

01 TELA-LANCAR-CONTA.
    05 BLANK SCREEN.
    05 LINE 3    COLUMN 30 VALUE "LANCAR CONTA.".
    05 LINE 6    COLUMN 28 VALUE "NUMERO DO DOC:".
    05 LINE 6    COLUMN 48 PIC 9(10) TO CP-NUM-DOC.
    05 LINE 8    COLUMN 28 VALUE "CNPJ DO FORNECEDOR:".
    05 LINE 8    COLUMN 48 PIC 9(14) TO CP-CNPJ-FORN.
    05 LINE 10   COLUMN 28 VALUE "DATA DE EMISSAO:".
    05 LINE 10   COLUMN 48 PIC 9(2) TO WS-DIA-EMISSAO.
    05 LINE 10   COLUMN 50 VALUE "/".
    05 LINE 10   COLUMN 51 PIC 9(2) TO WS-MES-EMISSAO.
    05 LINE 10   COLUMN 53 VALUE "/".
    05 LINE 10   COLUMN 54 PIC 9(4) TO WS-ANO-EMISSAO.
    05 LINE 12   COLUMN 28 VALUE "DATA DE VENCIMENTO:".
    05 LINE 12   COLUMN 48 PIC 9(2) TO WS-DIA-VENC.
    05 LINE 12   COLUMN 50 VALUE "/".
    05 LINE 12   COLUMN 51 PIC 9(2) TO WS-MES-VENC.
    05 LINE 12   COLUMN 53 VALUE "/".
    05 LINE 12   COLUMN 54 PIC 9(4) TO WS-ANO-VENC.
    05 LINE 14   COLUMN 28 VALUE "VALOR:".
    05 LINE 14   COLUMN 48 PIC 9(10).99 TO CP-VALOR.

01 TELA-ERRO.
    02 BLANK SCREEN.
    02 LINE 6 COLUMN 28     VALUE "ERRO:".
    02 LINE 6 COLUMN PLUS 1 PIC X(45) FROM WS-MENSAGEM.
    02 LINE 8 COLUMN 28     VALUE "PRESSIONE ENTER PARA CONTINUAR.".
    02 LINE 8 COLUMN PLUS 2 PIC X TO WS-CONFIRMA.

01 TELA-FINALIZAR.
    02 BLANK SCREEN.
    02 LINE 6 COLUMN 28     VALUE "CONTA LANCADA COM SUCESSO.".
    02 LINE 8 COLUMN 28     VALUE "PRESSIONE ENTER PARA CONTINUAR.".
    02 LINE 8 COLUMN PLUS 2 PIC X TO WS-CONFIRMA.


PROCEDURE DIVISION.
MAIN-PROCEDURE.
    PERFORM ABRIR-ARQUIVO.

    DISPLAY TELA-LANCAR-CONTA.
    ACCEPT  TELA-LANCAR-CONTA.

    PERFORM VALIDAR-FORNECEDOR.
    PERFORM VALIDAR-DATAS.
    PERFORM VALIDAR-VALOR.

    MOVE "A" TO CP-SITUACAO.

    WRITE CONTA-REG
        INVALID KEY
            MOVE "CONTA JA LANCADA" TO WS-MENSAGEM
            DISPLAY TELA-ERRO
            ACCEPT  TELA-ERRO
        NOT INVALID KEY
            DISPLAY TELA-FINALIZAR
            ACCEPT  TELA-FINALIZAR
    END-WRITE.

    CLOSE ARQ-CONTAS.
    EXIT PROGRAM.

ABRIR-ARQUIVO.
    OPEN I-O ARQ-CONTAS.
    IF FS-ARQ = "35"
        OPEN OUTPUT ARQ-CONTAS
        CLOSE ARQ-CONTAS
        OPEN I-O ARQ-CONTAS
    END-IF.
    IF FS-ARQ NOT = "00"
        DISPLAY TELA-BRANCA
        DISPLAY "ERRO AO ABRIR ARQUIVO: " FS-ARQ
        STOP RUN
    END-IF.

VALIDAR-FORNECEDOR.
    OPEN I-O ARQ-FORNECEDORES
    IF FS-ARQ NOT = "00"
        DISPLAY TELA-BRANCA
        DISPLAY "ERRO AO ABRIR ARQUIVO: " FS-ARQ
        STOP RUN
    END-IF.

    MOVE CP-CNPJ-FORN TO F-CNPJ.

    READ ARQ-FORNECEDORES
        INVALID KEY
            MOVE "FORNECEDOR NAO ENCONTRADO." TO WS-MENSAGEM
            DISPLAY TELA-ERRO
            ACCEPT TELA-ERRO
            STOP RUN
    END-READ.


VALIDAR-DATAS.
    COMPUTE CP-DATA-EMISSAO = WS-ANO-EMISSAO * 10000 + WS-MES-EMISSAO * 100 + WS-DIA-EMISSAO
    COMPUTE CP-DATA-VENC = WS-ANO-VENC * 10000 + WS-MES-VENC * 100 + WS-DIA-VENC
    IF CP-DATA-EMISSAO > CP-DATA-VENC
        MOVE "DATAS DE EMISSAO E VENCIMENTO INCOMPATIVEIS" TO WS-MENSAGEM
        DISPLAY TELA-ERRO
        ACCEPT  TELA-ERRO
        STOP RUN.

VALIDAR-VALOR.
    IF CP-VALOR < 0
        MOVE "VALOR NEGATIVO" TO WS-MENSAGEM
        DISPLAY TELA-ERRO
        ACCEPT  TELA-ERRO
        STOP RUN.
